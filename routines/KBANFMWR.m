KBANFMWR ; VEN/SMH - Fileman wrapper functions for EWDLite;2013-10-07  3:54 PM
 ;;
 ;
 ; wards1("DILIST",0)="14^*^0^"
 ; wards1("DILIST",0,"MAP")="IEN^.01"
 ; wards1("DILIST",1,0)="12^2-INTERMED"
 ; wards1("DILIST",2,0)="5^3 NORTH SURG"
 ; wards1("DILIST",3,0)="33^3E NORTH"
 ;
LIST(DDRFILE,DDRIENS,DDRFLDS,DDRFLAGS,DDRMAX,DDRFROM,DDRPART,DDRXREF,DDRSCRN,DDRID) ; Lister
 N DDROUT,DDRERR
 I $G(DDRFROM),$L(DDRFROM,"^")>1 N I F I=1:1:$L(DDRFROM,"^") S DDRFROM(I)=$P(DDRFROM,"^",I) ; flatten from
 I $G(DDRFLAGS)'["P" S DDRFLAGS="P"_$G(DDRFLAGS)
 D LIST^DIC(DDRFILE,$G(DDRIENS),$G(DDRFLDS),$G(DDRFLAGS),$G(DDRMAX),.DDRFROM,$G(DDRPART),$G(DDRXREF),$G(DDRSCRN),$G(DDRID),"DDROUT","DDRERR")
 ;
 ; ZEXCEPT: DIERR
 I $G(DIERR) Q $$arrayToJSON^%zewdJSON($NA(DDRERR))
 ;
 N JSONOUT
 S JSONOUT("map")=DDROUT("DILIST",0,"MAP")
 N I S I=0 F  S I=$O(DDROUT("DILIST",I)) Q:'I  S JSONOUT("data",I)=DDROUT("DILIST",I,0)
 QUIT $$arrayToJSON^%zewdJSON($NA(JSONOUT))
 ;
TEST w $$LIST(42,"","@;.01","PQ","","","","B","S D0=Y D WIN^DGPMDDCF I 'X","") QUIT
TEST2 w $$LIST(42) QUIT
TEST3 w $$LIST(50) QUIT
